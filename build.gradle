buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "1.12.2-" + getVersion()
group = "p455w0rd"
archivesBaseName = "EndermanEvolution"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
	version = "1.12.2-14.23.5.2768"
	runDir = "run"
	mappings = "snapshot_20171003"
	//useDepAts = true
	clientJvmArgs= ["-Dcofh.rf.crashOnOldAPI=false"]
}

repositories {
	mavenCentral()
	maven {
		name = "progwml6"
		url = "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name = "tterrag"
		url = "http://maven.tterrag.com"
	}
	maven {
		name = "tterrag2"
		url = "http://maven2.tterrag.com"
	}
	maven {
		name = "tehnut"
		url = "http://tehnut.info/maven"
	}
	maven {
		name = "ChickenBones"
		url "http://chickenbones.net/maven"
	}
	maven {
        name = "CoFH"
        url = "http://maven.covers1624.net"
    }
	maven {
		name = "Curse"
		url = "https://minecraft.curseforge.com/api/maven/"
	}
}

dependencies {
	deobfCompile "mezz.jei:jei_1.12.2:+"
	deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12+"
	deobfCompile "codechicken:CodeChickenLib:1.12.2-3.2.2.354:universal"
	deobfCompile "codechicken:EnderStorage:1.12.2-2.4.5.135:universal"
	deobfCompile "cpw.mods:ironchest:1.12.2-7.0.34.820"
	deobfCompile "cofh:RedstoneFlux:1.12-2.1.0.6:universal"
	deobfCompile "cofh:ThermalDynamics:1.12.2-2+:universal"
	deobfCompile "cofh:ThermalFoundation:1.12.2-2+:universal"
	deobfCompile "cofh:ThermalExpansion:1.12.2-5+:universal"
	deobfCompile "cofh:CoFHCore:1.12.2-4+:universal"
	deobfCompile "cofh:CoFHWorld:1.12.2-1+:universal"
	deobfCompile "slimeknights:TConstruct:1.12.2-2.10.1.518"
	deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.2.38"
	deobfCompile "immersive-engineering:ImmersiveEngineering:0.12:89"
	deobfCompile "p455w0rd:p455w0rdslib:1.12.2-2.0.36"
	deobfCompile "info.loenwind.autoconfig:AutoConfig:1.12.2-1.0.1"
	deobfCompile "info.loenwind.autosave:AutoSave:1.12.2-1.0.8"
	deobfCompile "com.enderio.core:EnderCore:1.12.2-0.5.44"
	deobfCompile ("com.enderio:EnderIO:1.12.2-5.0.39") {
		transitive = false
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
		
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	manifest {
	    attributes 'FMLAT': 'endermanevo_at.cfg'
	}
}

String getVersion() {
	String major = "0";
	String revision = "0";
	String patch = "0";
	String prefix = "public static final String VERSION = \"";
	File file = file("src/main/java/p455w0rd/endermanevo/init/ModGlobals.java")
	file.eachLine { String s ->
		s = s.trim();
		if (s.startsWith(prefix)) {
			s = s.substring(prefix.length(), s.length() - 2);
			String[] pts = s.split("\\.");

			major = pts[0];
			revision = pts[1];
			patch = pts[2];
		}
	}
	return "$major.$revision.$patch";
}
